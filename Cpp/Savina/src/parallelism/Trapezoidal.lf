/**
 * Copyright (C) 2020 TU Dresden
 * 
 * @author Hannes Klein
 * @author Felix Wittwer
 * @author Tassilo Tanneberger
 */

target Cpp {
    build-type : Release,
    no-runtime-validation: true,
};

import BenchmarkRunner from "../BenchmarkRunner.lf";

public preamble {=
    struct WorkMessage {
    
      double l;
      double r;
      double h;
    };
=}

reactor Manager(numWorkers:size_t(100), L:double(1.0), R:double(5.0), N:int(10000000)) {
    
    public preamble {=
        #include "reactor-cpp/logging.hh"
    =}
    
    state numTermsReceived:int(0);
    state resultArea:double(0.0);
    
    input inStart:void;
    output outFinished:void;
    
    output[numWorkers] outWorkers:{=WorkMessage=};
    input[numWorkers] inWorkers:{=double=};
    
    logical action sendWorkMessages:void;
    
    reaction(inStart) -> sendWorkMessages {=
        // reset local state
        numTermsReceived = 0;
        resultArea = 0.0;
        
        // start execution
        sendWorkMessages.schedule();
    =}
    
    reaction(sendWorkMessages) -> outWorkers {=
        double workerRange = (R - L) / numWorkers;
        double precision = (R - L) / N;
        for(int i = 0; i < outWorkers.size(); ++i) {
            double wl = (workerRange * i) + L;
            double wr = wl + workerRange;
            
            outWorkers[i].set(WorkMessage{wl,wr,precision});
        }
    =}
    
    reaction(inWorkers) -> outFinished {=
        for (auto i: inWorkers.present_indices_unsortedes()) {
            numTermsReceived += 1;
            resultArea += *inWorkers[i].get();
        }

        if(numTermsReceived == numWorkers) {
            reactor::log::Info() << "Area: " << resultArea;
            outFinished.set();
        }
    =}
}


reactor Worker {
    private preamble {=
        #include <cmath>
    =}
    
    input inManager:{=WorkMessage=};
    output outManager:{=double=};
    
    reaction(inManager) -> outManager {=
        double r = inManager.get()->r;
        double l = inManager.get()->l;
        double h = inManager.get()->h;
        int n = static_cast<int>( ((r - l) / h) );
        double accumArea = 0.0;
        
        int i = 0;
        while(i < n) {
            double lx = (i * h) + l;
            double rx = lx + h;
            
            double ly = fx(lx);
            double ry = fx(rx);
            
            double area = 0.5 * (ly + ry) * h;
            accumArea += area;
            
            i += 1;
        }
        
        outManager.set(accumArea);
    =}
    
    private preamble {=
        // the function to integrate
        double fx(double x) {
            
            double a = sin(pow(x, 3.0) - 1);
            double b = x + 1;
            double c = a / b;
            double d = sqrt(1.0 + exp(sqrt(2 * x)));
            double r = c * d;
            return r;
        }
    =}
}


main reactor (numIterations:int(12), numPieces:int(10000000), leftEndPoint:double(1.0), rightEndPoint:double(5.0), numWorkers:size_t(100)) {
    manager = new Manager(numWorkers=numWorkers, L=leftEndPoint, R=rightEndPoint, N=numPieces);
    runner = new BenchmarkRunner(numIterations=numIterations);
    
    runner.start -> manager.inStart;
    manager.outFinished -> runner.finished;
    
    reaction(startup) {=
        printBenchmarkInfo("TrapezoidalBenchmark");
        printArgs("numIterations", numIterations, "numPieces", numPieces, "leftEndPoint", leftEndPoint, "rightEndPoint", rightEndPoint, "numWorkers", numWorkers);
        printSystemInfo();
    =}
    
    workers = new[numWorkers] Worker();
    
    workers.outManager -> manager.inWorkers;
    manager.outWorkers -> workers.inManager;
}
